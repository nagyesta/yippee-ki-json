plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'checkstyle'
    id 'jacoco'
    id 'java'
    id 'maven-publish'
    id 'signing'
    //noinspection SpellCheckingInspection
    id "io.freefair.lombok" version "5.3.0"
    //noinspection SpellCheckingInspection
    id "io.toolebox.git-versioner" version "1.6.5"
    id "com.github.nagyesta.abort-mission-gradle-plugin" version "1.0.0"
    id "org.sonatype.gradle.plugins.scan" version "1.2.0"
}

group = 'com.github.nagyesta'
sourceCompatibility = '11'

project.ext {
    gitToken = project.hasProperty('githubToken') ? (project.property('githubToken') as String) : ''
    gitUser = project.hasProperty('githubUser') ? (project.property('githubUser') as String) : ''
    ossrhUser = project.hasProperty('ossrhUsername') ? (project.property('ossrhUsername') as String) : ''
    ossrhPass = project.hasProperty('ossrhPassword') ? (project.property('ossrhPassword') as String) : ''
    wikiProjectRoot = project.hasProperty('wikiProjectRoot')
            ? (project.property('wikiProjectRoot') as String)
            : "${buildDir}/reports/wiki/"
    yamlSchemaOutput = project.hasProperty('yamlSchemaOutput')
            ? (project.property('yamlSchemaOutput') as String)
            : "${projectDir}/schema/yippee-ki-json_config_schema.json"

    // dependency versions
    // implementation
    commonsIoVersion = '2.8.0'
    guavaVersion = '30.1-jre'
    javaxElVersion = '3.0.0'
    javaxInjectVersion = '1'
    jetbrainsAnnotationsVersion = '20.1.0'
    jsonPathVersion = '2.5.0'
    jsonSchemaValidatorVersion = '1.0.48'
    markdownGeneratorVersion = '1.3.1.1'

    // test
    wireMockVersion = '2.27.2'
    abortMissionVersion = '2.5.1'

    // tool
    jacocoVersion = '0.8.2'
}

versioner {
    startFrom {
        major = 0
        minor = 9
        patch = 0
    }
    pattern {
        pattern = "%M.%m.%p(.%c)"
    }
    git {
        authentication {
            https {
                token = project.ext.gitToken
            }
        }
    }
    tag {
        prefix = 'v'
        useCommitMessage = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.jayway.jsonpath:json-path'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.networknt:json-schema-validator'
    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.glassfish:javax.el'
    implementation 'javax.el:javax.el-api'
    implementation 'commons-io:commons-io'
    implementation 'com.google.guava:guava'
    implementation 'org.jetbrains:annotations'
    implementation 'javax.inject:javax.inject'
    implementation 'ch.qos.logback:logback-classic'
    implementation 'net.steppschuh.markdowngenerator:markdowngenerator'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'com.github.tomakehurst:wiremock'
    testImplementation 'com.github.nagyesta.abort-mission.boosters:abort.booster-junit-jupiter'
}

dependencyManagement {
    implementation {
        dependencies {
            dependency "javax.el:javax.el-api:${project.ext.javaxElVersion}"
            dependency "org.glassfish:javax.el:${project.ext.javaxElVersion}"
            dependency "com.jayway.jsonpath:json-path:${project.ext.jsonPathVersion}"
            dependency "com.networknt:json-schema-validator:${project.ext.jsonSchemaValidatorVersion}"
            dependency "org.jetbrains:annotations:${project.ext.jetbrainsAnnotationsVersion}"
            dependency "javax.inject:javax.inject:${project.ext.javaxInjectVersion}"
            dependency "commons-io:commons-io:${project.ext.commonsIoVersion}"
            dependency "com.google.guava:guava:${project.ext.guavaVersion}"
            dependency "net.steppschuh.markdowngenerator:markdowngenerator:${project.ext.markdownGeneratorVersion}"
        }
    }

    testImplementation {
        dependencies {
            dependency "com.github.tomakehurst:wiremock:${project.ext.wireMockVersion}"
            dependency "com.github.nagyesta.abort-mission.boosters:abort.booster-junit-jupiter:${project.ext.abortMissionVersion}"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        csv.enabled false
        html.enabled true
    }
}

abortMission.toolVersion project.ext.abortMissionVersion

test {
    useJUnitPlatform()
}
test.finalizedBy jacocoTestReport
jacocoTestReport.finalizedBy jacocoTestCoverageVerification

jacoco {
    toolVersion = project.ext.jacocoVersion
}

jacocoTestCoverageVerification {
    inputs.file(file("${buildDir}/reports/jacoco/report.xml"))
    outputs.file(file("${buildDir}/reports/jacoco/jacocoTestCoverageVerification"))

    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
            excludes = [
                    'com.github.nagyesta.yippeekijson.core.config.parser.JsonMapper',
                    'com.github.nagyesta.yippeekijson.metadata.schema.WikiConstants',
                    'com.github.nagyesta.yippeekijson.core.config.parser.raw.RawConfigParam',
                    'com.github.nagyesta.yippeekijson.core.config.validation.FileConstraintValidator',
                    'com.github.nagyesta.yippeekijson.core.control.FilePairProcessorController',
                    'com.github.nagyesta.yippeekijson.metadata.schema.parser.WikiMetadataParser'
            ]
        }
    }
    doLast {
        file("${buildDir}/reports/jacoco/jacocoTestCoverageVerification").write("Passed")
    }
}
bootJar.dependsOn check

versioner.apply()

java {
    withJavadocJar()
    withSourcesJar()
}

bootJar {
    archiveVersion.value(project.version as String)
}

task systemTestRunConvert(type: Exec) {
    inputs.file(file(bootJar.outputs.files.singleFile))
    inputs.file(file("${buildDir}/resources/test/json/example.json"))
    inputs.file(file("${buildDir}/resources/test/yaml/example.yml"))
    outputs.file(file("${buildDir}/test-results/systemTest/example.json"))
    dependsOn bootJar

    group = "Execution"
    //noinspection GroovyAccessibility
    description = "Run the output executable jar with ExecTask"
    commandLine "java", "-jar", bootJar.outputs.files.singleFile,
            "--yippee.config=${buildDir}/resources/test/yaml/example.yml",
            "--yippee.input=${buildDir}/resources/test/json/example.json",
            "--yippee.output=${buildDir}/test-results/systemTest/example.json",
            "--yippee.action=split-name"
    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.ERROR
}
task systemTestRunValidate(type: Exec) {
    inputs.file(file(bootJar.outputs.files.singleFile))
    inputs.file(file("${buildDir}/resources/test/validation/validation-input.json"))
    inputs.file(file("${buildDir}/resources/test/validation/test-schema.json"))
    inputs.file(file("${buildDir}/resources/test/yaml/all-rules.yml"))
    outputs.file(file("${buildDir}/test-results/systemTest/validation-output.json"))
    dependsOn bootJar

    group = "Execution"
    //noinspection GroovyAccessibility
    description = "Run the output executable jar with ExecTask"
    commandLine "java", "-jar", bootJar.outputs.files.singleFile,
            "--yippee.config=${buildDir}/resources/test/yaml/all-rules.yml",
            "--yippee.input=${buildDir}/resources/test/validation/validation-input.json",
            "--yippee.output=${buildDir}/test-results/systemTest/validation-output.json",
            "--yippee.action=validate"
    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.ERROR
}
task systemTest {
    dependsOn systemTestRunConvert
    dependsOn systemTestRunValidate
    inputs.file(file("${buildDir}/test-results/systemTest/example.json"))
    inputs.file(file("${buildDir}/resources/test/json/example-split.json"))
    inputs.file(file("${buildDir}/test-results/systemTest/validation-output.json"))
    inputs.file(file("${buildDir}/resources/test/validation/validation-output.json"))
    outputs.file(file("${buildDir}/test-results/systemTest/result"))
    doLast {
        if (file("${buildDir}/test-results/systemTest/example.json").text
                != file("${buildDir}/resources/test/json/example-split.json").text) {
            throw new StopExecutionException("System test (convert) assertion failed.")
        }
        if (file("${buildDir}/test-results/systemTest/validation-output.json").text
                != file("${buildDir}/resources/test/validation/validation-output.json").text) {
            throw new StopExecutionException("System test (validate) assertion failed.")
        }
        file("${buildDir}/test-results/systemTest/result").write("Passed")
    }
}
task generateYamlSchema(type: Exec) {
    inputs.file(file(bootJar.outputs.files.singleFile))
    outputs.file(file(project.property("yamlSchemaOutput")))
    dependsOn bootJar

    group = "Execution"
    //noinspection GroovyAccessibility
    description = "Run the output executable jar with ExecTask"
    commandLine "java", "-jar", bootJar.outputs.files.singleFile,
            "--yippee.export-yml-schema=true",
            "--yippee.output=${project.property("yamlSchemaOutput")}"
    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.ERROR
}
task generateWikiMarkdown(type: Exec) {
    inputs.file(file(bootJar.outputs.files.singleFile))
    outputs.dir(file(project.property("wikiProjectRoot")))
    dependsOn bootJar

    group = "Execution"
    //noinspection GroovyAccessibility
    description = "Run the output executable jar with ExecTask"
    commandLine "java", "-jar", bootJar.outputs.files.singleFile,
            "--yippee.export-markdown=true",
            "--yippee.output-directory=${project.property("wikiProjectRoot")}"
    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.ERROR
}

tasks.withType(Checkstyle) {
    configProperties = [base_dir: rootDir.toString(), cache_file: file("${buildDir}/checkstyle/cacheFile")]
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile(rootProject.file('config/checkstyle/checkstyle-stylesheet.xsl'))
    }
}

task includeSchema(type: Copy) {
    from file("${projectDir}/schema/yippee-ki-json_config_schema.json")
    into file("${buildDir}/resources/main")
}

processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
    finalizedBy includeSchema
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/nagyesta/yippee-ki-json")
            credentials {
                username = project.ext.gitUser
                password = project.ext.gitToken
            }
        }
        maven {
            name = "ossrh"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = project.ext.ossrhUser
                password = project.ext.ossrhPass
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact bootJar
            pom {
                name = 'Yippee-Ki-JSON'
                description = 'A minimal JSON manipulation app built on Spring Boot and JSON Path.'
                url = 'https://github.com/nagyesta/yippee-ki-json'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/nagyesta/yippee-ki-json/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'nagyesta'
                        name = 'Istvan Zoltan Nagy'
                        url = 'https://github.com/nagyesta/'
                    }
                }
                scm {

                    connection = 'scm:git:https://github.com/nagyesta/yippee-ki-json.git'
                    developerConnection = 'scm:git:https://github.com/nagyesta/yippee-ki-json.git'
                    url = 'https://github.com/nagyesta/yippee-ki-json/'
                }
                withXml {
                    asNode().dependencies.findAll() {
                        final def parent = it.parent()
                        parent.remove(it)
                    }
                    asNode().dependencyManagement.findAll() {
                        final def parent = it.parent()
                        parent.remove(it)
                    }
                }
            }
        }
    }
}

//Disable metadata publishing and rely on Maven only
//noinspection GroovyAssignabilityCheck
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    sign publishing.publications.mavenJava
}
